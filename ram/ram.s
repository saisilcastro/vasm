IER = $600E				; interrupt Enable register
IFR = $600D				; interrupt Flag Register
PCR = $600C				; peripheral control register
DDRA = $6003			; data direction bus A
DDRB = $6002			; data direction bus B
PORTA = $6001
PORTB = $6000

kb_write = $03FD
kb_flag  = $03FE
kb_read	 = $03FF

RELEASE = %00000001
SHIFT	= %00000010

kb_buffer = $0400

	.ORG $8000
setup:
	LDX #$FF
    TXS
	
	LDA #$01			; select rising edge
	STA PCR

	LDA #$82			; enable CA1
	STA IER	
	CLI

    LDA #%11111111
    STA DDRB           ; Set PORTB as output
    LDA #%00000000
	STA DDRA			; Set PORTA for control signals

	LDA #00
	STA kb_write
	STA kb_flag
	STA counter + 1
	STA kb_read
	
	JSR lcd_init_4bits
	
loop:	
	SEI
	LDA kb_read
	CMP kb_write
	CLI
	BNE key_pressed
	JMP loop

key_pressed:
	LDX kb_read
	LDA kb_buffer, X
	JSR lcd_byte
	INC kb_read
	JMP loop

IRQ_KEY:
	PHA
	TXA
	PHA

	LDA kb_flag
	AND #RELEASE
	BNE has_released

	LDA PORTA
	TAX

	CMP #$F0
	BEQ release_key
	
	CMP #$66
	BEQ erase_byte
	CMP #$12
	BEQ shift_down
	CMP #$59
	BEQ shift_down

	LDA kb_flag
	AND #SHIFT
	BNE shifted_key

	LDA keymap, X
	JMP push_key

shifted_key:
	LDA keymap_shifted, X

push_key:
	LDX kb_write
	STA kb_buffer, X
	INC kb_write
	JMP IRQ_KEY_QUIT
shift_down:
	LDA kb_flag
	ORA #SHIFT
	STA kb_flag
	JMP IRQ_KEY_QUIT
shift_up:
	LDA kb_flag
	EOR #SHIFT
	STA kb_flag
	JMP IRQ_KEY_QUIT 
erase_byte:
	LDA #01
	JSR lcd_instruction
	LDA kb_write
	BEQ IRQ_KEY_QUIT
	DEC kb_write
	LDA #0
	STA kb_read
	JMP IRQ_KEY_QUIT
has_released:
	LDA kb_flag
	EOR #RELEASE
	STA kb_flag

	LDA PORTA

	CMP #$12
	BEQ shift_up
	CMP #$59
	BEQ shift_up

	JMP IRQ_KEY_QUIT
release_key:
	LDA kb_flag
	ORA #RELEASE
	STA kb_flag
IRQ_KEY_QUIT:
	PLA
	TAX
	PLA
	RTI
nmi:
	RTI

	.include "lcd-4bits.s"
	.include "binary-2-decimal.s"
	.ORG $FD00
keymap:
	.byte "????????????? `?" ; 00-0F
	.byte "?????q1???zsaw2?" ; 10-1F
	.byte "?cxde43?? vftr5?" ; 20-2F
	.byte "?nbhgy6???mju78?" ; 30-3F
	.byte "?,kio09??./l;p-?" ; 40-4F
	.byte "??'?[=?????]?\??" ; 50-5F
	.byte "?????????1?47???" ; 60-6F
	.byte "0.2568???+3-*9??" ; 70-7F
	.byte "????????????????" ; 80-8F
	.byte "????????????????" ; 90-9F
	.byte "????????????????" ; A0-AF
	.byte "????????????????" ; B0-BF
	.byte "????????????????" ; C0-CF
	.byte "????????????????" ; D0-DF
	.byte "????????????????" ; E0-EF
	.byte "????????????????" ; F0-FF
keymap_shifted:
	.byte "????????????? ~?" ; 00-0F
	.byte "?????Q!???ZSAW@?" ; 10-1F
	.byte "?CXDE#$?? VFTR%?" ; 20-2F
	.byte "?NBHGY^???MJU&*?" ; 30-3F
	.byte "?<KIO)(??>?L:P_?" ; 40-4F
	.byte '??"?{+?????}?|??' ; 50-5F
	.byte "?????????1?47???" ; 60-6F
	.byte "0.2568???+3-*9??" ; 70-7F
	.byte "????????????????" ; 80-8F
	.byte "????????????????" ; 90-9F
	.byte "????????????????" ; A0-AF
	.byte "????????????????" ; B0-BF
	.byte "????????????????" ; C0-CF
	.byte "????????????????" ; D0-DF
	.byte "????????????????" ; E0-EF
	.byte "????????????????" ; F0-FF

	.ORG $FFFA
	.WORD nmi
	.WORD setup
	.WORD IRQ_KEY
